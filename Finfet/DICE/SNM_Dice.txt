* TOPOLOGIA DICE

.TITLE "SNM DICE 0.5Ghz"

.OPTION POST = 2
.PRINT DC v(x0) v(x1) v(x2) v(x3)

.include 7nm_TT.pm

.param fin = 1

* TENSAO DO CIRCUITO
.param supply = 0.7

.global supply gnd

* SUPPLY DO CIRCUITO
Vvdd vdd gnd DC supply

* ==================== SNM
Vvn vn gnd DC 0

* Vx0 x0 vn DC 0
* Vx2 x2 vn DC 0

Vx1 x1 vn DC 0
Vx3 x3 vn DC 0

* ==== HOLD
* WL = 0
* Vwl wl gnd DC 0
* Vbl bl gnd DC supply
* Vblb blb gnd DC supply

* ==== READ 0
* WL = 1 BL e BL = 1
Vwl wl gnd DC supply
Vbl bl gnd DC supply
Vblb blb gnd DC supply

* ==== WRITE 1
* WL = 1 BL = 0 BLB = 1
* Vwl wl gnd DC supply
* Vbl bl gnd DC supply
* Vblb blb gnd DC 0

* DICE

* INVERSOR 0
MP0 vdd x3 x0 vdd pmos_rvt nfin = fin
MN0 x0 x1 gnd gnd nmos_rvt nfin = fin

* INVERSOR 1
MP1 vdd x0 x1 vdd pmos_rvt nfin = fin
MN1 x1 x2 gnd gnd nmos_rvt nfin = fin

* INVERSOR 2
MP2 vdd x1 x2 vdd pmos_rvt nfin = fin
MN2 x2 x3 gnd gnd nmos_rvt nfin = fin

* INVERSOR 3
MP3 vdd x2 x3 vdd pmos_rvt nfin = fin
MN3 x3 x0 gnd gnd nmos_rvt nfin = fin

* TRANSISTORES DE PASSAGEM
MN5 x1 wl bl gnd nmos_rvt nfin = fin
MN7 x3 wl bl gnd nmos_rvt nfin = fin

MN6 x2 wl blb gnd nmos_rvt nfin = fin
MN4 x0 wl blb gnd nmos_rvt nfin = fin

.DC Vvn 0 'supply' 'supply*0.001'

.end
