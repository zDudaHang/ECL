* CIRCUITO 6T
.TITLE 'Array 6T 128 celulas FINFET'

.OPTION POST = 2

.include 7nm_TT.pm

.include Escrita.txt
.include Leitura.txt
.include PreCarga.txt

* PARAMETROS
.param fin_auxiliares = 1
.param fin = 1
.param num_cel = 128

* TENSAO DO CIRCUITO
.param supply = 0.4

* TEMPO
.param clock = 1g
.param slope = '0.01*period'
.param period = '1/clock'
.param step = 'period/7'

 .global supply gnd

* FONTE DO CIRCUITO
Vvdd vdd gnd DC supply

* ========= SIMULACAO
Vwl0    wl0     gnd     PWL(0ns 0)
Vwl1    wl1     gnd     PWL(0ns 0)

* ==== HOLD:

* Vbit    bit     gnd     PWL(0ns 0)
* Vwe     we      gnd     PWL(0ns 0)
* Vwl     wl      gnd     PWL(0ns 0)
* Vpre    pre     gnd     PWL(0ns 0)
* Vsae    sae     gnd     PWL(0ns 0)

* ==== WRITE: ESCREVER O VALOR OPOSTO -> ESCREVER O VALOR ALVO E INSERIR A FALHA NO TEMPO

* VALOR INICIAL DO 'Q' = 0V
* Vbit bit gnd PWL(0ns supply 'period' supply 'period + slope' 0)

* VALOR INICIAL DO 'Q' = Supply
*Vbit bit gnd PWL(0ns 0 'period' 0 'period + slope' supply)
*
* Vwl wl gnd PWL(0ns 0 '3*step' 0 '3*step + slope' supply '4*step' supply '4*step + slope' 0
* + 'period + 3*step' 0 'period + 3*step + slope' supply 'period + 4*step' supply 'period + 4*step + slope' 0)
*
* Vwe we gnd PWL(0ns supply '2*period' supply '2*period + slope' 0)
*
* Vpre pre gnd PWL(0ns 0 'step' 0 'step + slope' supply '6*step' supply '6*step + slope' 0
* + 'period + step' 0 'period + step + slope' supply 'period + 6*step' supply 'period + 6*step + slope' 0)
*
* Vsae sae gnd PWL(0ns 0)

* === READ: ESCREVER O VALOR E LER EM 1.44ns

* VALOR INICIAL DO Q = SUPPLY
Vbit bit gnd PWL(0ns 0)

* VALOR INICIAL DO Q = 0
* Vbit bit gnd PWL(0ns supply)

Vwl wl gnd PWL(0ns 0 '3*step' 0 '3*step + slope' supply '4*step' supply '4*step + slope' 0
+ 'period + 3*step' 0 'period + 3*step + slope' supply 'period + 4*step' supply 'period + 4*step + slope' 0)

Vwe we gnd PWL(0ns supply 'period' supply 'period + slope' 0)

Vpre pre gnd PWL(0ns 0 'step' 0 'step + slope' supply '6*step' supply '6*step + slope' 0
+ 'period + step' 0 'period + step + slope' supply 'period + 6*step' supply 'period + 6*step + slope' 0)

Vsae sae gnd PWL(0ns 0 'period + 3*step' 0 'period + 3*step + slope' supply 'period + 4*step' supply 'period + 4*step + slope' 0)

* DECLARACAO DO CIRCUITO

* CIRCUITOS AUXILIARES

* PRE-CARGA
Xprecarga pre bl blb vdd gnd pre_carga fin=fin_auxiliares

* LEITURA
Xleitura  sae bl blb out outb vdd gnd leitura fin=fin_auxiliares

* ESCRITA
Xescrita  bit we bl blb vdd gnd escrita fin=fin_auxiliares

* ====== ARRAY DA 6T

* === CELULA TESTE COM VALOR 1

* PRIMEIRO INVERSOR
MP0 vdd qb q vdd pmos_rvt nfin = fin
MN0 q qb gnd gnd nmos_rvt nfin = fin

* SEGUNDO INVERSOR
MP1 vdd q qb vdd pmos_rvt nfin = fin
MN1 qb q gnd gnd nmos_rvt nfin = fin

* TRANSISTORES DE PASSAGEM
MN2 bl wl q gnd nmos_rvt nfin = fin
MN3 blb wl qb gnd nmos_rvt nfin = fin

* === 63 CELULAS COM VALOR 1

* PRIMEIRO INVERSOR
MP4 vdd q0b q0 vdd pmos_rvt nfin ='fin*((num_cel/2)-1)'
MN4 q0 q0b gnd gnd nmos_rvt nfin ='fin*((num_cel/2)-1)'

* SEGUNDO INVERSOR
MP5 vdd q0 q0b vdd pmos_rvt nfin ='fin*((num_cel/2)-1)'
MN5 q0b q0 gnd gnd nmos_rvt nfin ='fin*((num_cel/2)-1)'

* TRANSISTORES DE PASSAGEM
MN6 bl wl0 q0 gnd nmos_rvt nfin ='fin*((num_cel/2)-1)'
MN7 blb wl0 q0b gnd nmos_rvt nfin ='fin*((num_cel/2)-1)'

* === 64 CELULAS COM VALOR 0

* PRIMEIRO INVERSOR
MP8 vdd q1b q1 vdd pmos_rvt nfin ='fin*(num_cel/2)'
MN8 q1 q1b gnd gnd nmos_rvt nfin ='fin*(num_cel/2)'

* SEGUNDO INVERSOR
MP9 vdd q1 q1b vdd pmos_rvt nfin ='fin*(num_cel/2)'
MN9 q1b q1 gnd gnd nmos_rvt nfin ='fin*(num_cel/2)'

* TRANSISTORES DE PASSAGEM
MN10 bl wl1 q1 gnd nmos_rvt nfin ='fin*(num_cel/2)'
MN11 blb wl1 q1b gnd nmos_rvt nfin ='fin*(num_cel/2)'


* ====== INICIALIZACOES

* === CELULA TESTE
.ic v(q) = supply

* === 63 CELULAS
.ic v(q0) = supply

* === 64 CELULAS
.ic v(q1) = 0

* RESULTADOS

* TESTE DE ROBUSTEZ
* 010 : gnd nodo
* 101 : nodo gnd
Iexp gnd q exp(0 6u 1.44n 10p 1.44001n 320p)

.tran 0.01ns '4*period'

.end
