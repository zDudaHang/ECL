* TOPOLOGIA PDICE

.options post = 2

.include 32nm_HP.pm

.include Escrita.txt
.include PreCarga.txt

* PARAMETROS

* NODO
.param length = 32n

* TENSAO DO CIRCUITO
.param supply = 0.9

* DIMENSOES DA CELULA
.param width = 64n

* CIRCUITOS AUXILIARES
.param width_aux = 210n

* TEMPOS
.param clock = 1g
.param slope = 1p
.param period = 1/clock
.param step = period/7

.global supply gnd

* SUPPLY DO CIRCUITO
Vvdd vdd gnd DC supply

.param vboost = 'supply + 0.6'

* SIMULACAO

Vbit bit gnd PWL(0ns vboost 'period' vboost 'period + slope' 0)

* =============== WE ANTIGO
*Vwe we gnd PWL(0ns 0 '2*step' 0 '2*step + slope' vboost '5*step' vboost '5*step + slope' 0
*+ '2*period + 2*step' 0 '2*period + 2*step + slope' vboost '2*period + 5*step' vboost '2*period + 5*step + slope' 0)

Vwe we gnd PWL(0ns supply 1ns supply 1.1ns 0 2ns 0 2.1ns supply 3ns supply 3.1ns 0)

Vwwl wwl gnd PWL(0ns 0 '3*step' 0 '3*step + slope' vboost '4*step' vboost '4*step + slope' 0
+ '2*period + 3*step' 0 '2*period + 3*step + slope' vboost '2*period + 4*step' vboost '2*period + 4*step + slope' 0)

Vrwl rwl gnd PWL(0ns 0 'period + step' 0 'period + step + slope' supply 'period + 4*step' supply 'period + 4*step + slope' 0
+ '3*period + step' 0 '3*period + step + slope' supply '3*period + 4*step' supply '3*period + 4*step + slope' 0)

Vpre pre gnd PWL(0ns 0 'step' 0 'step + slope' vboost '6*step' vboost '6*step + slope' 0
+ 'period + step' 0 'period + step + slope' supply 'period + 6*step' supply 'period + 6*step + slope' 0
+ '2*period + step' 0 '2*period + step + slope' vboost '2*period + 6*step' vboost '2*period + 6*step + slope' 0
+ '3*period + step' 0 '3*period + step + slope' supply '3*period + 6*step' supply '3*period + 6*step + slope' 0)

* DECLARACAO DO CIRCUITO

*  CIRCUITOS AUXILIARES

* PRE-CARGA
Xprecarga pre bl blb vdd gnd pre_carga width=width_aux length=length

* ESCRITA
Xescrita  bit we bl blb vdd gnd escrita width=width_aux length=length

* PDICE

* INVERSOR 0
MP0 vdd x3 x0 vdd PMOS w = width l = length
MN0 x0 x1 gnd gnd NMOS w = width l = length

* INVERSOR 1
MP1 vdd x0 x1 vdd PMOS w=width l=length
MN1 x1 x2 gnd gnd NMOS w=width l=length

* INVERSOR 2
MP2 vdd x1 x2 vdd PMOS w=width l=length
MN2 x2 x3 gnd gnd NMOS w=width l=length

* INVERSOR 3
MP3 vdd x2 x3 vdd PMOS w=width l=length
MN3 x3 x0 gnd gnd NMOS w=width l=length

* TRANSISTORES DE PASSAGEM
MN5 x1 wwl bl gnd NMOS w = width l = length
MN6 x3 wwl bl gnd NMOS w = width l = length

MN7 bl rwl between7_8 gnd NMOS w = width l = length
MN10 blb rwl between10_9 gnd NMOS w = width l = length

MN8 between7_8 x1 gnd gnd NMOS w = width l = length
MN9 between10_9 x2 gnd gnd NMOS w = width l = length

Cbl bl gnd 20f

* VALOR INICIAL DA CELULA
.ic v(x0) = supply

* RESULTADOS

.tran 0.01ns '4*period'

.measure tran Escrita_0 trig v(wwl) val='supply/2' rise=1 targ v(x0) val='supply/2' fall=1
.measure tran Leitura_0 trig v(rwl) val='supply/2' rise=1 targ v(bl) val='supply*0.9' fall=1

.measure tran Escrita_1 trig v(wwl) val='supply/2'  rise=2 targ v(x0) val='supply/2' rise=1
.measure tran Leitura_1 trig v(rwl) val='supply/2'  rise=2 targ v(blb) val='supply*0.9' fall=3

.measure tran Energia_vdd INTEG i(Vvdd) from=0.0 to='4*period'

.end
