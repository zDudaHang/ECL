* TOPOLOGIA DICE

.TITLE "DICE 0.5Ghz"

.OPTION POST = 2
.PRINT v(x0) v(x1) v(x2) v(x3) v(bl) v(blb) v(bit) v(we) v(wl) v(pre)

.include 7nm_TT.pm

.include Escrita.txt
.include PreCarga.txt

* PARAMETROS
.param fin_auxiliares = 1
.param fin = 1

* TENSAO DO CIRCUITO
.param supply = 0.7

* TEMPO
.param clock = 0.5g
.param slope = '0.01*period'
.param period = '1/clock'
.param step = 'period/7'

.global supply gnd

* SUPPLY DO CIRCUITO
Vvdd vdd gnd DC supply

* SIMULACAO

Vbit bit gnd PWL(0ns supply 'period' supply 'period + slope' 0)

Vwe we gnd PWL(0ns supply 'period' supply 'period + slope' 0
+ '2*period' 0 '2*period + slope' supply
+ '3*period' supply '3*period + slope' 0)

Vwl wl gnd PWL(0ns 0 '3*step' 0 '3*step + slope' supply '4*step' supply '4*step + slope' 0
+ 'period + 3*step' 0 'period + 3*step + slope' supply 'period + 4*step' supply 'period + 4*step + slope' 0
+ '2*period + 3*step' 0 '2*period + 3*step + slope' supply '2*period + 4*step' supply '2*period + 4*step + slope' 0
+ '3*period + 3*step' 0 '3*period + 3*step + slope' supply '3*period + 4*step' supply '3*period + 4*step + slope' 0)

Vpre pre gnd PWL(0ns 0 'step' 0 'step + slope' supply '6*step' supply '6*step + slope' 0
+ 'period + step' 0 'period + step + slope' supply 'period + 6*step' supply 'period + 6*step + slope' 0
+ '2*period + step' 0 '2*period + step + slope' supply '2*period + 6*step' supply '2*period + 6*step + slope' 0
+ '3*period + step' 0 '3*period + step + slope' supply '3*period + 6*step' supply '3*period + 6*step + slope' 0)

* DECLARACAO DO CIRCUITO

*  CIRCUITOS AUXILIARES

* PRE-CARGA
Xprecarga pre bl blb vdd gnd pre_carga fin=fin_auxiliares

* ESCRITA
Xescrita  bit we bl blb vdd gnd escrita fin=fin_auxiliares

* DICE

* INVERSOR 0
MP0 vdd x3 x0 vdd pmos_rvt nfin = fin
MN0 x0 x1 gnd gnd nmos_rvt nfin = fin

* INVERSOR 1
MP1 vdd x0 x1 vdd pmos_rvt nfin = fin
MN1 x1 x2 gnd gnd nmos_rvt nfin = fin

* INVERSOR 2
MP2 vdd x1 x2 vdd pmos_rvt nfin = fin
MN2 x2 x3 gnd gnd nmos_rvt nfin = fin

* INVERSOR 3
MP3 vdd x2 x3 vdd pmos_rvt nfin = fin
MN3 x3 x0 gnd gnd nmos_rvt nfin = fin

* TRANSISTORES DE PASSAGEM
MN5 x1 wl bl gnd nmos_rvt nfin = fin
MN7 x3 wl bl gnd nmos_rvt nfin = fin

MN6 x2 wl blb gnd nmos_rvt nfin = fin
MN4 x0 wl blb gnd nmos_rvt nfin = fin

* CAPACITORES
Cblb blb gnd 20f
Cbl bl gnd 20f

* VALOR INICIAL DA CELULA
.ic v(x0) = supply
.ic v(x1) = 0
.ic v(x2) = supply
.ic v(x3) = 0

* RESULTADOS

.tran 0.01ns '4*period'

.measure tran Escrita_0 trig v(wl) val='supply/2' rise=1 targ v(x0) val='supply/2' fall=1
.measure tran Leitura_0 trig v(wl) val='supply/2' rise=2 targ v(blb) val='supply*0.7' fall=2
.measure tran Escrita_1 trig v(wl) val='supply/2'  rise=3 targ v(x0) val='supply/2' rise=1
.measure tran Leitura_1 trig v(wl) val='supply/2'  rise=4 targ v(bl) val='supply*0.7' fall=2
.measure tran Energia_vdd INTEG i(Vvdd) from=0.0 to='4*period'


.end
