* CIRCUITO PDICE

.options post = 2

.include 7nm_TT.pm

.include Escrita.txt
.include PreCarga.txt

* PARAMETROS
.param fin = 1

* TENSAO DO CIRCUITO
.param supply = 0.7

* TEMPO
.param clock = 1g
.param slope = 1p
.param period = 1/clock
.param step = period/7

 .global supply gnd

* FONTE DO CIRCUITO
Vvdd vdd gnd DC supply

.param vboost = 'supply + 0.6'

* SIMULACAO

Vbit bit gnd PWL(0ns vboost)

Vwe we gnd PWL(0ns 0 '2*step' 0 '2*step + slope' vboost '5*step' vboost '5*step + slope' 0)

Vwwl wwl gnd PWL(0ns 0 '3*step' 0 '3*step + slope' vboost '4*step' vboost '4*step + slope' 0)

Vrwl rwl gnd PWL(0ns 0)

Vpre pre gnd PWL(0ns 0 'step' 0 'step + slope' vboost '6*step' vboost '6*step + slope' 0)

* DECLARACAO DO CIRCUITO

* CIRCUITOS AUXILIARES

* PRE-CARGA
Xprecarga pre bl blb vdd gnd pre_carga fin=fin

* ESCRITA
Xescrita  bit we bl blb vdd gnd escrita fin=fin

* PDICE

* INVERSOR 0
MP0 vdd x3 x0 vdd pmos_rvt nfin = fin
MN0 x0 x1 gnd gnd nmos_rvt nfin = fin

* INVERSOR 1
MP1 vdd x0 x1 vdd pmos_rvt nfin = fin
MN1 x1 x2 gnd gnd nmos_rvt nfin = fin

* INVERSOR 2
MP2 vdd x1 x2 vdd pmos_rvt nfin = fin
MN2 x2 x3 gnd gnd nmos_rvt nfin = fin

* INVERSOR 3
MP3 vdd x2 x3 vdd pmos_rvt nfin = fin
MN3 x3 x0 gnd gnd nmos_rvt nfin = fin

* TRANSISTORES DE PASSAGEM
MN5 x1 wwl bl gnd nmos_rvt nfin = fin
MN6 x3 wwl bl gnd nmos_rvt nfin = fin

MN7 bl rwl between7_8 gnd nmos_rvt nfin = fin
MN10 blb rwl between10_9 gnd nmos_rvt nfin = fin

MN8 between7_8 x1 gnd gnd nmos_rvt nfin = fin
MN9 between10_9 x2 gnd gnd nmos_rvt nfin = fin

* VALOR INICIAL DA CELULA
.ic v(x0) = supply

Cbl bl gnd 10f
Cblb blb gnd 10f

* RESULTADOS

.tran 0.01ns 'period'

.measure tran Escrita_0 trig v(wwl) val='supply/2' rise=1 targ v(x0) val='supply/2' fall=1

.measure tran Energia_vdd INTEG i(Vvdd) from=0 to='period'

.end
