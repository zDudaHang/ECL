* CIRCUITO DICE

.options post = 2

.include 7nm_TT.pm

.include Escrita.txt
.include PreCarga.txt

* PARAMETROS
.param length = 21n
.param fin = 1

* TENSAO DO CIRCUITO
.param supply = 0.7

* DIMENSAO DA CELULA
.param width = 70n
.param width_aux = 210n

* TEMPO
.param clock = 1g
.param slope = 1p
.param period = 1/clock
.param step = period/7

 .global supply gnd

* FONTE DO CIRCUITO
Vvdd vdd gnd DC supply

* SIMULACAO

Vbit bit gnd PWL(0ns 0)

Vwe we gnd PWL(0ns 0)

Vwl wl gnd PWL(0ns 0 '3*step' 0 '3*step + slope' supply '4*step' supply '4*step + slope' 0)

Vpre pre gnd PWL(0ns 0 'step' 0 'step + slope' supply '6*step' supply '6*step + slope' 0)

* DECLARACAO DO CIRCUITO

* CIRCUITOS AUXILIARES

* PRE-CARGA
Xprecarga pre bl blb vdd gnd pre_carga width=width_aux length=length fin=fin

* ESCRITA
Xescrita  bit we bl blb vdd gnd escrita width=width_aux length=length fin=fin

* DICE

* INVERSOR 0
MP0 vdd x3 x0 vdd pmos_rvt w = width l = length nfin = fin
MN0 x0 x1 gnd gnd nmos_rvt w = width l = length nfin = fin

* INVERSOR 1
MP1 vdd x0 x1 vdd pmos_rvt w = width l = length nfin = fin
MN1 x1 x2 gnd gnd nmos_rvt w = width l = length nfin = fin

* INVERSOR 2
MP2 vdd x1 x2 vdd pmos_rvt w = width l = length nfin = fin
MN2 x2 x3 gnd gnd nmos_rvt w = width l = length nfin = fin

* INVERSOR 3
MP3 vdd x2 x3 vdd pmos_rvt w = width l = length nfin = fin
MN3 x3 x0 gnd gnd nmos_rvt w = width l = length nfin = fin

* TRANSISTORES DE PASSAGEM
MN5 x1 wl bl gnd nmos_rvt w = width l = length nfin = fin
MN7 x3 wl bl gnd nmos_rvt w = width l = length nfin = fin

MN6 x2 wl blb gnd nmos_rvt w = width l = length nfin = fin
MN4 x0 wl blb gnd nmos_rvt w = width l = length nfin = fin

* VALOR INICIAL DA CELULA
.ic v(x0) = supply

* RESULTADOS

.tran 0.01ns 'period'

.measure tran Leitura_1 trig v(wl) val='supply/2'  rise=1 targ v(bl) val='supply*0.9' fall=1

.measure tran Energia_vdd INTEG i(Vvdd) from=0 to='period'

.end
