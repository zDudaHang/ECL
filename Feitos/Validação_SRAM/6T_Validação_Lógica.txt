*** Célula SRAM

.include 32nm_HP.pm

* Nodo tecnológico
	.param length = 32n

* Tensão do circuito
	.param supply = 0.9

* Numero de células na coluna
	.param num_cell = 128

* Dimensionamento da célula
	.param w_m1m2 = 70n
	.param w_m3m4 = 70n
	.param w_m5m6 = 70n
	.param supply = 0.9

* Dimensionamento do circuito de escrita
	.param w_me_pmos = 210n
	.param w_me_nmos = 210n

* Dimensionamento do sense amplifier
	.param w_sa = 210n
	
* Dimensionamento da pré-carga
	.param w_pc = 210n
	
* Tempo de sinal alto ou baixo
	.param ts_r = 1/(7*clock)
*iniciar com 1ps
	.param ts_w = 1/(7*clock)

	
* Clock (g: ghz, meg:mhz, k: khz)
	.param clock = 1g
	.param slope = 0.01*unit
	.param unit = 1/clock
*periodo para 0.25GHz ->2ns
*slope 1ps

	
*** Fontes DC
	Vcell	vcell	    gnd	    DC	    supply
	Vvdd	vdd			gnd		DC		supply
	Vwl2	wl2			gnd		DC		0
	Vwl22	wl22		gnd		DC		0

*** Ondas

Vbit   	bit    	gnd	PWL	(0n  0  '2*unit'  0  '(2*unit)+slope'  supply  '4*unit' supply)

Vwe	we	gnd	PWL 	(0n  0  
	+	'2*ts_w'  0  '2*ts_w+slope'  supply  '5*ts_w'  supply  '5*ts_w+slope'  0  '6*ts_w'  0
	+ 	'2*unit+2*ts_w'  0  '2*unit+2*ts_w+slope'  supply  '2*unit+5*ts_w'  supply 	'2*unit+5*ts_w+slope'  0  '2*unit+6*ts_w'  0
	+ 	)
	
Vwl 	wl	gnd	PWL	(0n  0  
	+ 	'3*ts_w'  0  '(3*ts_w)+slope'  supply  '(4*ts_w)'  supply  '(4*ts_w)+slope'  0  '5*ts_w'  0  
	+	'(1*unit)+(2*ts_r)'  0  '(1*unit)+(2*ts_r)+slope'  supply  '(1*unit)+(3*ts_r)'  supply  '(1*unit)+(3*ts_r)+slope'  0  '(1*unit)+(5*ts_r)'  0
	+	'(2*unit)+(3*ts_w)'  0  '(2*unit)+(3*ts_w)+slope'  supply  '(2*unit)+(4*ts_w)'  supply  '(2*unit)+(4*ts_w)+slope'  0  '(2*unit)+(5*ts_w)'  0
	+	'(3*unit)+(2*ts_r)'  0  '(3*unit)+(2*ts_r)+slope'  supply  '(3*unit)+(3*ts_r)'  supply  '(3*unit)+(3*ts_r)+slope'  0  '(3*unit)+(5*ts_r)'  0
	+	)

Vpre 	pre	gnd	PWL 	(0n  0 
	+	ts_w  0  'ts_w+slope'  supply  '(6*ts_w)'  supply  '6*ts_w+slope'  0  '7*ts_w'  0
	+  	'(1*unit)+ts_r'  0  '(1*unit)+ts_r+slope'  supply  '(1*unit)+(6*ts_r)'  supply  '(1*unit)+(6*ts_r)+slope'  0  '(1*unit)+(7*ts_r)'  0
	+	'(2*unit)+ts_w'  0  '(2*unit)+ts_w+slope'  supply  '(2*unit)+(6*ts_w)'  supply  '(2*unit)+(6*ts_w)+slope'  0  '(2*unit)+(7*ts_w)'  0
	+	'(3*unit)+ts_r'  0  '(3*unit)+ts_r+slope'  supply  '(3*unit)+(6*ts_r)'  supply  '(3*unit)+(6*ts_r)+slope'  0  '(3*unit)+(7*ts_r)'  0
	+	)
	
Vsae 	sae	gnd	PWL 	(0n  0  
	+	'(1*unit)+(4*ts_r)'  0  '(1*unit)+(4*ts_r)+slope'  supply  '(1*unit)+(5*ts_r)'  supply  '(1*unit)+(5*ts_r)+slope'  0  '(1*unit)+(6*ts_r)'  0
	+	'(3*unit)+(4*ts_r)'  0  '(3*unit)+(4*ts_r)+slope'  supply  '(3*unit)+(5*ts_r)'  supply  '(3*unit)+(5*ts_r)+slope'  0  '(3*unit)+(6*ts_r)'  0
	+	)
	
*** Valor inicial da célula de teste
	.ic  v(q)=supply


*** Valores iniciais das 127 células em hold
	.ic  v(q2)=0   	 	v(qb2)=supply
	.ic  v(q22)=supply  v(qb22)=0  


*** Netlist célula 6T
* Inversor 1
	Mm3	vcell  	q   	qb  	vcell	PMOS  	w=w_m3m4	l=length
	Mm1	qb		q		gnd		gnd    	NMOS	w=w_m1m2	l=length

* Inversor 2
	Mm4	vcell	qb		q		vcell	PMOS	w=w_m3m4	l=length
	Mm2	q		qb		gnd 	gnd		NMOS	w=w_m1m2	l=length

* Transistores de passagem
	Mm6	blb		wl		qb		gnd		NMOS	w=w_m5m6	l=length
	Mm5	bl		wl		q		gnd		NMOS	w=w_m5m6	l=length


*** Netlist circuito de escrita
* Inversor Bit
	Minv_bit1	vdd		bit		bitb	vdd		PMOS	w=w_me_pmos	l=length	
	Minv_bit2	bitb	bit		gnd		gnd		NMOS	w=w_me_nmos	l=length

* Inversor Write Enable
	Minv_we1	vdd	we	web	vdd	PMOS	w=w_me_pmos	l=length	
	Minv_we2	web	we	gnd	gnd	NMOS	w=w_me_nmos	l=length

* Escreve em BL
	Mme1	vdd		bitb	xe1		vdd		PMOS	w=w_me_pmos	l=length
	Mme2	gnd		bitb	xe1		gnd		NMOS	w=w_me_nmos	l=length
	Mme3	xe1		we		bl		gnd		NMOS	w=w_me_nmos	l=length
	Mme4	xe1		web		bl		vdd		PMOS	w=w_me_pmos	l=length

* Escreve em BLB
	Mme5	vdd		bit		xe2		vdd		PMOS	w=w_me_pmos	l=length
	Mme6	gnd		bit		xe2		gnd		NMOS	w=w_me_nmos	l=length
	Mme7	xe2		we		blb		gnd		NMOS	w=w_me_nmos	l=length
	Mme8	xe2		web		blb		vdd		PMOS	w=w_me_pmos	l=length


*** Sense Amplifier
* Transistores de passagem
	Mmsa2	blb		sae		outb 	vdd		PMOS	w=w_sa	l=length
	Mmsa1	bl		sae		out		vdd		PMOS	w=w_sa	l=length

* Inversor 1

	Mmsa3	vdd   	out   	outb  	vdd 	PMOS  	w=w_sa  l=length
	Mmsa5	outb	out		xsa1	gnd   	NMOS	w=w_sa	l=length

* Inversor 2
	Mmsa4	vdd		outb	out		vdd		PMOS	w=w_sa	l=length
	Mmsa6	out		outb	xsa1  	gnd		NMOS	w=w_sa	l=length

* Transistor de passagem gnd
	Mmsa7   xsa1	sae		gnd		gnd		NMOS	w=w_sa	l=length

* Capacitores para as saídas do sense amplifier
	Cload	out		gnd		1f
	Cload2	outb	gnd		1f


*** Pré-carga
Xpcarga pre bl blb vdd Precarga

.subckt Precarga pre bl blb vdd 
	Mpc1	vdd		pre		bl		vdd		PMOS	w= w_pc 	l=length
	Mpc2	vdd		pre		blb		vdd		PMOS	w= w_pc 	l=length
	Mpc3	blb		pre		bl		vdd		PMOS	w= w_pc 	l=length
.end subckt
	
*** 63 Celulas 6T (Q = 0)
* Inversor 1
	MP12	vdd   	q2   	qb2  	vdd		PMOS  	w='w_m3m4*63'    	l=length
	MN12	qb2		q2		gnd		gnd    	NMOS	w='w_m1m2*63'		l=length

* Inversor 2
	MP22	vdd		qb2		q2		vdd		PMOS	w='w_m3m4*63'		l=length
	MN22	q2		qb2		gnd 	gnd	  	NMOS	w='w_m1m2*63'		l=length

* Transistores de passagem
	MN32	blb		wl2		qb2		gnd		NMOS	w='w_m5m6*63'		l=length
	MN42	bl		wl2		q2		gnd	    NMOS	w='w_m5m6*63'		l=length
***


*** 64 Celulas 6T (Q = 1)
* Inversor 1
	MP122	vdd   	q22   	qb22  	vdd		PMOS  	w='w_m3m4*64'    	l=length
	MN122	qb22	q22		gnd		gnd    	NMOS	w='w_m1m2*64'		l=length

* Inversor 2
	MP222	vdd		qb22	q22		vdd		PMOS	w='w_m3m4*64'		l=length
	MN222	q22		qb22	gnd 	gnd	  	NMOS	w='w_m1m2*64'		l=length

* Transistores de passagem
	MN322	blb		wl22	qb22	gnd		NMOS	w='w_m5m6*64'		l=length
	MN422	bl		wl22	q22		gnd	    NMOS	w='w_m5m6*64'		l=length
***
	

*** Tipo de simulação	
	.tran '0.005*unit' '4*unit'	

*** Obtenção de resultados
* Escrita 0
	.measure  tran  Escrita_0  trig v(wl) val='supply/2'  rise=1  targ v(q) val='supply/2' fall=1 
* Leitura  0
	.measure  tran  Leitura_0  trig v(wl) val='supply/2'  rise=2  targ v(bl) val='supply*0.9' fall=2
* Escrita 1
	.measure  tran  Escrita_1  trig v(wl) val='supply/2'  rise=3  targ v(q) val='supply/2' rise=1
* Leitura 1
	.measure  tran  Leitura_1  trig v(wl) val='supply/2'  rise=4  targ v(blb) val='supply*0.9' fall=4
* Energia (Energia consumida durante todo tempo de simulação)
	.measure tran 	Energia_vcell 	INTEG i(Vcell) from=0 to='4*unit'
	

.control		
	run
	
	set noaskquit
	set color0=white 	**white background
	set color1=black 	**black grid
	set xbrushwidth=2 	**linewidth for grid, border and graph

	*quit
.endc


.end
