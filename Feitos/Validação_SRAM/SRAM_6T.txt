*** VALIDAÇÃO SRAM 6T

*** INCLUDES:

* TECNOLOGIA:
.include 32nm_HP.pm

* SUB-CIRCUITOS:
.include Circuito_Escrita.txt
.include Circuito_Leitura.txt
.include Pre_Carga.txt

*** PARÂMETROS:

* NODO:
.param length = 32n

* TENSÃO CIRCUITO:
.param supply = 0.9

* DIMENSÕES CÉLULA:
.param w_m1m2 = 70n
.param w_m3m4 = 70n
.param w_m5m6 = 70n

* DIMENSÕES CIRCUITO DE ESCRITA:
.param w_esc_pmos = 210n
.param w_esc_nmos = 210n

* DIMENSÃO DO SENSE AMPLIFIER:
.param w_sae = 210n

* DIMENSÃO DA PRÉ-CARGA:
.param w_pc = 210n

* TEMPO:
* CLOCK = 0.25GHz
* SLOPE = 1ps
* ts_r e ts_w = 0.5ns
* Tempo de estabilização do clock = 2ns

*** FONTES CIRCUITO:

* CIRCUITO (SUPPLY):
Vdd vdd gnd DC supply

* SIMULAÇÃO

Vbit bit gnd PWL(0ns 0 8ns 0 8.001ns supply 16ns supply)

Vwe we gnd PWL(0ns 0 1ns 0 1.001ns supply
3ns supply 3.001ns 0 9ns 0 9.001ns supply
10.5ns supply 10.501ns 0)

Vwl wl gnd PWL(0ns 0 1.5ns 0 1.501 supply 2ns supply 2.001ns 0
5ns 0 5.001ns supply 6.5ns supply 6.501ns 0
9.5ns 0 9.501ns supply 10ns supply 10.001ns 0
13ns 0 13.001ns supply 14.5ns supply 14.501ns 0)

Vpre pre gnd PWL(0ns 0 0.5ns 0 0.501ns supply 3ns supply 3.001ns 0
4.5ns 0 4.501ns supply 7ns supply 7.001ns 0
8.5ns 0 8.501ns supply 11ns supply 11.001ns 0
12.5ns 0 12.501ns supply 15ns supply 15.001ns 0)

Vsae sae gnd PWL(0ns 0 5.5ns 0 5.501 supply 6ns supply 6.001ns 0
13.5ns 0 13.501ns supply 14ns supply 14.001ns 0)


*** DECLARAÇÃO DO CIRCUITO:

** SUB-CIRCUITOS:

* PRÉ-CARGA
Xpre_carga pre_carga pre bl blb vdd gnd Precarga

* LEITURA
Xleitura leitura sae bl blb out outb vdd gnd Leitura

* ESCRITA
Xescrita escrita bit we bl blb vdd gnd Escrita

** CIRCUITO 6T

* INVERSOR 1
M3 vdd qb q vdd PMOS w =w_m3m4 l =length
M1 q qb gnd gnd NMOS w =w_m1m2 l =length

* TRANSISTORES DE PASSAGEM
M5 q WL bl gnd NMOS w =w_m5m6 l =length
M6 blb wl qb gnd NMOS w =w_m5m6  l =length

* INVERSOR 2
M4 vdd q qb vdd PMOS w =w_m3m4 l =length
M2 qb q gnd gnd NMOS w =w_m1m2  l =length

** RESULTADOS

.measure tran Escrita_0 trig v(wl) val='supply/2' rise=1 targ v(q) val='supply/2' fall=1
.measure tran Leitura_0 trig v(wl) val='supply/2' rise=2 targ v(bl) val='supply*0.9' fall-2
.measure tran Escrita_1 trig v(wl) val='supply/2'  rise=3 targ v(q) val='supply/2' rise=1
.measure tran Leitura_1 trig v(wl) val='supply/2'  rise=4 targ v(blb) val='supply*0.9' fall=4

.measure tran Energia_vdd INTEG i(Vdd) from=0 to=17ns

.tran 0.01ns 17ns

.end
