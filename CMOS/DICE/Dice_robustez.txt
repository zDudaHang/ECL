.options post = 2

.include 32nm_HP.pm

.include Escrita.txt
.include PreCarga.txt

* PARAMETROS

* NODO
.param length = 32n

* TENSAO DO CIRCUITO
.param supply = 0.9

* DIMENSOES DA CELULA
.param width = '2*length'

* CIRCUITOS AUXILIARES
.param width_aux = 210n

* TEMPO
.param clock = 1g
.param slope = '0.01*period'
.param period = '1/clock'
.param step = 'period/7'

.global supply gnd

* SUPPLY DO CIRCUITO
Vvdd vdd gnd DC supply

* SIMULACAO
Vwl      wl      gnd     PWL(0ns 0)
Vbit     bit     gnd     PWL(0ns 0)
Vwe      we      gnd     PWL(0ns 0)
Vpre     pre     gnd     PWL(0ns 0)
Vbl      bl      gnd     PWL(0ns supply)
Vblb     blb     gnd     PWL(0ns supply)

* DECLARACAO DO CIRCUITO

*  CIRCUITOS AUXILIARES

* PRE-CARGA
Xprecarga pre bl blb vdd gnd pre_carga width=width_aux length=length

* ESCRITA
Xescrita  bit we bl blb vdd gnd escrita width=width_aux length=length

* DICE

* INVERSOR 0
MP0 vdd x3 x0 vdd PMOS w = width l = length
MN0 x0 x1 gnd gnd NMOS w = width l = length

* INVERSOR 1
MP1 vdd x0 x1 vdd PMOS w = width l = length
MN1 x1 x2 gnd gnd NMOS w = width l = length

* INVERSOR 2
MP2 vdd x1 x2 vdd PMOS w = width l = length
MN2 x2 x3 gnd gnd NMOS w = width l = length

* INVERSOR 3
MP3 vdd x2 x3 vdd PMOS w = width l = length
MN3 x3 x0 gnd gnd NMOS w = width l = length

* TRANSISTORES DE PASSAGEM
MN5 x1 wl bl gnd NMOS w = width l = length
MN7 x3 wl bl gnd NMOS w = width l = length

MN6 x2 wl blb gnd NMOS w = width l = length
MN4 x0 wl blb gnd NMOS w = width l = length

Cblb blb gnd 20f
Cbl bl gnd 20f

* VALOR INICIAL DA CELULA
.ic v(x0) = 0
.ic v(x1) = supply

* TESTE DE ROBUSTEZ
* 010 : gnd nodo
* 101 : nodo gnd
Iexp gnd x0 exp(0 10u 0.1ns 10p 0.10001ns 320p)

.tran 0.01ns '4*period'

.end
