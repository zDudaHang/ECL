** --------------------------------------------------- VALIDACAO DAC(Dummy Active Cell)
** OBS: A DAC não contém a operação de escrita, logo não foi considerada nessa validação. Logo, o circuito de escrita foi retirado.

** --------------------------------------------------- INCLUDES

* TECNOLOGY
.include 32nm_HP.pm

* SUB-CIRCUITOS
.include Leitura_Dice.txt
.include Pre_Carga.txt

** --------------------------------------------------- PARAMETROS

* NODO
.param length = 32n

* TENSAO CIRCUITO
.param supply = 0.9

* DIMENSOES DA CELULA
.param width = 70n

* TEMPO
* t_s = tempo de subida = tempo de descida
.param clock = 1g
.param t_s = 1ps
.param periodo = 1/clock
.param unidade = periodo/7

.global supply gnd

** ---------------------------------------------------FONTES DO CIRCUITO

Vdd vdd gnd DC supply

** --------------------------------------------------- SIMULACAO

Vrwl rwl gnd PWL(0ns 0 'unidade' 0 'unidade + t_s' supply '5*unidade' supply '5*unidade + t_s' 0)

Vpre pre gnd PWL(0ns 0 'unidade' 0 'unidade + t_s' supply '6*unidade' supply '6*unidade + t_s' 0)

** --------------------------------------------------- DECLARACAO DO CIRCUITO

* SUB-CIRCUITOS

* PRE-CARGA
Xprecarga pre bl blb vdd gnd pre_carga

* LEITURA
Xleitura sae bl blb out outb vdd gnd leitura

* CIRCUITO DAC:
* q1 = gnd, x0 = gnd

* INVERSOR 1
MP0 vdd q2 gnd vdd PMOS w=width l=length 
MN0 gnd q0 gnd gnd NMOS w=width l=length 

* INVERSOR 2
MP1 vdd gnd q0 vdd PMOS w=width l=length 
MN1 q0 gnd gnd gnd NMOS w=width l=length 

* INVERSOR 3
MP2 vdd q0 gnd vdd PMOS w=width l=length 
MN2 gnd q2 gnd gnd NMOS w=width l=length 

* INVERSOR 4
MP3 vdd gnd q2 vdd PMOS w=width l=length 
MN3 q2 gnd gnd gnd NMOS w=width l=length 

* TRANSISTORES DE PASSAGEM
MN5 q0 gnd bl gnd NMOS w=width l=length 
MN6 q2 gnd bl gnd NMOS w=width l=length 

MN7 bl rwl q7_8 gnd NMOS w=width l=length 
MN8 q7_8 q0 gnd gnd NMOS w=width l=length 
MN9 gnd gnd q9_10 gnd NMOS w=width l=length 
MN10 q9_10 rwl blb gnd NMOS w=width l=length 

** --------------------------------------------------- RESULTADOS

.tran 0.01ns 'periodo'

.measure tran Leitura_0 trig v(rwl) val='supply/2' rise=1 targ v(bl) val='supply*0.9' fall=1


.measure tran Energia_vdd INTEG i(Vdd) from=0 to='periodo'

.control		
	run
	set noaskquit
	set color0=white 	**white background
	set color1=black 	**black grid
	set xbrushwidth=2 	**linewidth for grid, border and graph
	*quit
.endc

.end
