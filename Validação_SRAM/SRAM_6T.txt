*** VALIDAÇÃO SRAM 6T

*** INCLUDES:

* TECNOLOGIA:
.include 32nm_HP.pm

* SUB-CIRCUITOS:
.include Circuito_Escrita.txt
.include Circuito_Leitura.txt
.include Pre_Carga.txt

*** PARÂMETROS:

* NODO:
.param length = 32n

* TENSÃO CIRCUITO:
.param supply = 0.9

* DIMENSÕES CÉLULA:
.param w_m1m2 = 70n
.param w_m3m4 = 70n
.param w_m5m6 = 70n


* TEMPO:
.param clock = 1g
.param slope = 1ps
.param period = 1/clock
.param step = period/7

***
.global supply gnd

*** FONTES CIRCUITO:

* CIRCUITO (SUPPLY):
Vdd vdd gnd DC supply

* SIMULAÇÃO

Vbit bit gnd PWL(0ns 0 '2*period' 0 '(2*period)+slope' supply)

Vwe we gnd PWL(0ns 0 '2*step' 0 '2*step + slope' supply '5*step' supply '5*step + slope' 0
+ '2*period + 2*step' 0 '2*period + 2*step + slope' supply '2*period + 5*step' supply '2*period + 5*step + slope' 0)

Vwl wl gnd PWL(0ns 0 '3*step' 0 '3*step + slope' supply '4*step' supply '4*step + slope' 0
+ 'period + 2*step' 0 'period + 2*step + slope' supply 'period + 5*step' supply 'period + 5*step + slope' 0
+ '2*period + 3*step' 0 '2*period + 3*step + slope' supply '2*period + 4*step' supply '2*period + 4*step + slope' 0
+ '3*period + 2*step' 0 '3*period + 2*step + slope' supply '3*period + 5*step' supply '3*period + 5*step + slope' 0)

Vpre pre gnd PWL(0ns 0 'step' 0 'step + slope' supply '6*step' supply '6*step + slope' 0
+ 'period + step' 0 'period + step + slope' supply 'period + 6*step' supply 'period + 6*step + slope' 0
+ '2*period + step' 0 '2*period + step + slope' supply '2*period + 6*step' supply '2*period + 6*step + slope' 0
+ '3*period + step' 0 '3*period + step + slope' supply '3*period + 6*step' supply '3*period + 6*step + slope' 0)

Vsae sae gnd PWL(0ns 0 'period + 3*step' 0 'period + 3*step + slope' supply 'period + 4*step' supply 'period + 4*step + slope' 0
+ '3*period + 3*step' 0 '3*period + 3*step + slope' supply '3*period + 4*step' supply '3*period + 4*step + slope' 0)


*** DECLARAÇÃO DO CIRCUITO:
** SUB-CIRCUITOS:

* PRÉ-CARGA
Xprecarga pre bl blb vdd gnd pre_carga

* LEITURA
Xleitura  sae bl blb out outb vdd gnd leitura

* ESCRITA
Xescrita  bit we bl blb vdd gnd escrita

** CIRCUITO 6T

* INVERSOR 1
M3 vdd qb q vdd PMOS w =w_m3m4 l =length
M1 q qb gnd gnd NMOS w =w_m1m2 l =length

* TRANSISTORES DE PASSAGEM
M5 q WL bl gnd NMOS w =w_m5m6 l =length
M6 blb wl qb gnd NMOS w =w_m5m6  l =length

* INVERSOR 2
M4 vdd q qb vdd PMOS w =w_m3m4 l =length
M2 qb q gnd gnd NMOS w =w_m1m2  l =length

** RESULTADOS

.tran 0.01ns '4*period'


.measure tran Escrita_0 trig v(wl) val='supply/2' rise=1 targ v(q) val='supply/2' fall=1
.measure tran Leitura_0 trig v(wl) val='supply/2' rise=2 targ v(bl) val='supply*0.9' fall=2
.measure tran Escrita_1 trig v(wl) val='supply/2'  rise=3 targ v(q) val='supply/2' rise=1
.measure tran Leitura_1 trig v(wl) val='supply/2'  rise=4 targ v(blb) val='supply*0.9' fall=4

.measure tran Energia_vdd INTEG i(Vdd) from=0 to='4*period'

.control		
	run
	set noaskquit
	set color0=white 	**white background
	set color1=black 	**black grid
	set xbrushwidth=2 	**linewidth for grid, border and graph
	*quit
.endc


.end
